plugins {
	id "architectury-plugin" version "3.1-SNAPSHOT"
	id "dev.architectury.loom" version "0.7.2-SNAPSHOT" apply false
	id "com.matthewprenger.cursegradle" version "1.4.0" apply false
	id "maven-publish"
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply from: "https://files.latmod.com/public/markdown-git-changelog.gradle"

	archivesBaseName = rootProject.archives_base_name
	def ENV = System.getenv()
	version = "${mod_version}-build.${ENV.BUILD_NUMBER ?: 9999}"
	group = rootProject.maven_group

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"

		// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
		// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
		// We'll use that if it's available, but otherwise we'll use the older option.
		def targetVersion = 8
		if (JavaVersion.current().isJava9Compatible()) {
			options.release = targetVersion
		}
	}

	java {
		withSourcesJar()
	}

	repositories {
		mavenLocal()
		maven {
			url "https://www.cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}
		maven {
			url "https://maven.shedaniel.me"
		}
		maven {
			url "https://maven.terraformersmc.com"
		}
	}

}

task curseforgePublish
