plugins {
	id "com.github.johnrengelman.shadow" version "7.1.0"
}

architectury {
	platformSetupLoomIde()
	neoForge()
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentNeoForge.extendsFrom common
	dev
}

loom {
	accessWidenerPath = project(":common").file("src/main/resources/ftbultimine.accesswidener")
}

artifacts {
	dev(jar)
}

repositories {
	maven {
		url "https://maven.neoforged.net/releases/"
	}
}

dependencies {
	neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
	modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_api_version}"

	modApi("dev.ftb.mods:ftb-library-neoforge:${rootProject.ftb_library_version}") { transitive = false }
	modApi("dev.ftb.mods:ftb-ranks-neoforge:${rootProject.ftb_ranks_version}") { transitive = false }
	modApi("dev.ftb.mods:ftb-ez-crystals:${rootProject.ftb_ez_crystals_version}") { transitive = false }

	common(project(path: ":common", configuration: "dev")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }
}

apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/extra-local-mods.gradle"

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/neoforge.mods.toml") {
		expand "version": project.version,
				"archversion": project.architectury_api_version,
				"neoforgeversionrange": project.neoforge_version_range,
				"neoforgeloaderversion": project.neoforge_loader_version,
				"mcversion": project.minecraft_version,
				"ftblibraryversion": project.ftb_library_version
	}
}

shadowJar {
	exclude "fabric.mod.json"

	configurations = [project.configurations.shadowCommon]
	setArchiveClassifier("dev-shadow")
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveBaseName.set "${rootProject.archives_base_name}-${project.name}"
	archiveClassifier.set null
	atAccessWideners.add('ftbultimine.accesswidener')
}

jar {
	setArchiveClassifier("dev")
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

publishing {
	publications {
		mavenNeoforge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			version ftbPublishing.mavenVersion
			from components.java
		}
	}
}
